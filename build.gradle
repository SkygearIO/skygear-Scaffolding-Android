// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

File[] fileList(String dir) {
    file(dir).listFiles({file -> file.isFile() } as FileFilter).sort()
}

task updateAppSettings << {
    def TemplateDir = "template/"
    def AppDir = "app/src/main/java/io/skygear/skygear_starter_project/"
    def endpoint = ""
    def apiKey = ""

    endpoint = System.console().readLine(
            "\nWhat is your skygear endpoint (You can find it in portal)?" +
            "\nExample: https://myapp.skygeario.com/" +
            "\n> "
    )

    while (endpoint.trim().length() == 0) {
        endpoint = System.console().readLine("> ");
    }

    apiKey = System.console().readLine(
            "\nWhat is your skygear API key (You can find it in portal)?" +
            "\nExample: dc0903fa85924776baa77df813901efc" +
            "\n> "
    )

    while (apiKey.trim().length() == 0) {
        apiKey = System.console().readLine("> ");
    }

    copy {
        from(TemplateDir)
        into(AppDir)
        include('MyApplication.java')
        filter(ReplaceTokens, tokens:[ENDPOINT: endpoint, APIKEY: apiKey])
    }
}

